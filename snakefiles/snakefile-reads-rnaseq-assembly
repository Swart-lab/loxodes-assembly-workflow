# Rules for combined Trinity assembly of RNAseq

rule link_trinity_assem:
    # symlink to latest assemblies folder
    input:
        assembly="assembly/trinity_{options}/trinity_outdir/Trinity.fasta",
        gene_trans_map="assembly/trinity_{options}/trinity_outdir/Trinity.fasta.gene_trans_map"
    output:
        assembly="assembly/latest/trinity_{options}.Trinity.fasta",
        gene_trans_map="assembly/latest/trinity_{options}.Trinity.fasta.gene_trans_map"
    shell:
        "ln -sr {input.assembly} {output.assembly};"
        "ln -sr {input.gene_trans_map} {output.gene_trans_map}"

# combined assembly of all libraries in a given experiment
rule trinity_rnaseq_nochlamy_comb:
    input:
        reads=lambda wildcards: expand("data/reads-rnaseq-filter/{lib}_R12_ktrim_qtrim28_bbmap_nochlamy.R12.fq.gz",lib=config["experiments_rnaseq"][wildcards.experiment]["libraries"]),
        samplesfile=lambda wildcards: config["experiments_rnaseq"][wildcards.experiment]["sample_file"]
    threads: 16
    conda: "envs/trinity.yml"
    params:
        sslibtype="RF",
        prefix="assembly/trinity_rnaseq_{experiment}_nochlamy_comb/trinity_outdir",
        # Using full paths for the following parameters because they will be
        # passed to HpcGridRunner. Use HpcGridRunner version commit 6a44b7b
        hpcgridrunnerpath="/ebio/abt2_projects/ag-swart-loxodes/opt/HpcGridRunner/hpc_cmds_GridRunner.pl",
        hpcgridrunnerconfigtemplate="/ebio/abt2_projects/ag-swart-loxodes/workflow/hpcgridrunner_sge_conf_trinity_template"
    output:
        assembly="assembly/trinity_rnaseq_{experiment}_nochlamy_comb/trinity_outdir/Trinity.fasta",
        gene_trans_map="assembly/trinity_rnaseq_{experiment}_nochlamy_comb/trinity_outdir/Trinity.fasta.gene_trans_map",
        hpcgridrunnerconfig="/ebio/abt2_projects/ag-swart-loxodes/assembly/trinity_rnaseq_{experiment}_nochlamy_comb/hpcgridrunner_sge_conf_trinity"
    shell:
        # Use commas in the sed regex because CONDA_PREFIX contains slashes
        # This command puts the current Conda environment (for running Trinity) 
        # into the config file for HpcGridRunner. This is because the current
        # conda environment is not automatically 'inherited' by HpcGridRunner 
        # array jobs.
        "sed \"s,PLACEHOLDER,$CONDA_PREFIX,\" {params.hpcgridrunnerconfigtemplate} > {output.hpcgridrunnerconfig};"
        # For the Trinity command, it is important to have --grid_exec as last 
        # parameter and to avoid using = to link option and value in the params.
        # otherwise there is a weird bug in the file `recursive_trinity.cmds`
        # where unexpected parameters are introduced into the commands
        "Trinity --seqType fq --max_memory 64G --CPU {threads} --SS_lib_type {params.sslibtype} --samples_file {input.samplesfile} --output {params.prefix} --grid_exec \"{params.hpcgridrunnerpath} -G {output.hpcgridrunnerconfig} -c\";"

rule trinity_rnaseq_nochlamy_mapped_comb:
    # Trinity assembly of RNAseq reads mapped to a reference genome
    input:
        readsf=lambda wildcards: expand("data/reads-rnaseq-genome-mapped/{lib}_q28_nochlamy.{ref_params}.R1.fastq.gz",lib=config["experiments_rnaseq"][wildcards.experiment]["libraries"], ref_params=wildcards.ref_params),
        samplesfile="workflow/trinity_samples_rnaseq_{experiment}_nochlamy_comb.mapped_{ref_params}.txt"
    threads: 16
    conda: "envs/trinity.yml"
    params:
        sslibtype="RF",
        prefix="assembly/trinity_rnaseq_{experiment}_nochlamy_comb.mapped_{ref_params}/trinity_outdir",
        # Using full paths for the following parameters because they will be
        # passed to HpcGridRunner. Use HpcGridRunner version commit 6a44b7b
        hpcgridrunnerpath="/ebio/abt2_projects/ag-swart-loxodes/opt/HpcGridRunner/hpc_cmds_GridRunner.pl",
        hpcgridrunnerconfigtemplate="/ebio/abt2_projects/ag-swart-loxodes/workflow/hpcgridrunner_sge_conf_trinity_template"
    output:
        assembly="assembly/trinity_rnaseq_{experiment}_nochlamy_comb.mapped_{ref_params}/trinity_outdir/Trinity.fasta",
        gene_trans_map="assembly/trinity_rnaseq_{experiment}_nochlamy_comb.mapped_{ref_params}/trinity_outdir/Trinity.fasta.gene_trans_map",
        hpcgridrunnerconfig="/ebio/abt2_projects/ag-swart-loxodes/assembly/trinity_rnaseq_{experiment}_nochlamy_comb.mapped_{ref_params}/hpcgridrunner_sge_conf_trinity"
    shell:
        # Use commas in the sed regex because CONDA_PREFIX contains slashes
        # This command puts the current Conda environment (for running Trinity) 
        # into the config file for HpcGridRunner. This is because the current
        # conda environment is not automatically 'inherited' by HpcGridRunner 
        # array jobs.
        "sed \"s,PLACEHOLDER,$CONDA_PREFIX,\" {params.hpcgridrunnerconfigtemplate} > {output.hpcgridrunnerconfig};"
        # For the Trinity command, it is important to have --grid_exec as last 
        # parameter and to avoid using = to link option and value in the params.
        # otherwise there is a weird bug in the file `recursive_trinity.cmds`
        # where unexpected parameters are introduced into the commands
        "Trinity --seqType fq --max_memory 64G --CPU {threads} --SS_lib_type {params.sslibtype} --samples_file {input.samplesfile} --output {params.prefix} --grid_exec \"{params.hpcgridrunnerpath} -G {output.hpcgridrunnerconfig} -c\";"

"""
rule trinity_rnaseq_nochlamy_mapped_comb_gg:
    # genome-guided Trinity assembly of RNAseq reads mapped to a reference genome
    input:
        # TODO combine and sort bam files for all libraries assigned to this experiment
        sortbam="annotation/{ref_params}/mapping/hisat2.{lib}_q28_nochlamy.{ref_params}.sort.bam"
    threads: 16
    conda: "envs/trinity.yml"
    params:
        sslibtype="RF",
        prefix="assembly/trinity_rnaseq_{experiment}_nochlamy_comb.gg_{ref_params}/trinity_outdir",
        # Using full paths for the following parameters because they will be
        # passed to HpcGridRunner. Use HpcGridRunner version commit 6a44b7b
        hpcgridrunnerpath="/ebio/abt2_projects/ag-swart-loxodes/opt/HpcGridRunner/hpc_cmds_GridRunner.pl",
        hpcgridrunnerconfigtemplate="/ebio/abt2_projects/ag-swart-loxodes/workflow/hpcgridrunner_sge_conf_trinity_template"
    output:
        assembly="assembly/trinity_rnaseq_{experiment}_nochlamy_comb.gg_{ref_params}/trinity_outdir/Trinity.fasta",
        gene_trans_map="assembly/trinity_rnaseq_{experiment}_nochlamy_comb.gg_{ref_params}/trinity_outdir/Trinity.fasta.gene_trans_map",
        hpcgridrunnerconfig="/ebio/abt2_projects/ag-swart-loxodes/assembly/trinity_rnaseq_{experiment}_nochlamy_comb.gg_{ref_params}/hpcgridrunner_sge_conf_trinity"
    shell:
        # Use commas in the sed regex because CONDA_PREFIX contains slashes
        # This command puts the current Conda environment (for running Trinity) 
        # into the config file for HpcGridRunner. This is because the current
        # conda environment is not automatically 'inherited' by HpcGridRunner 
        # array jobs.
        "sed \"s,PLACEHOLDER,$CONDA_PREFIX,\" {params.hpcgridrunnerconfigtemplate} > {output.hpcgridrunnerconfig};"
        # For the Trinity command, it is important to have --grid_exec as last 
        # parameter and to avoid using = to link option and value in the params.
        # otherwise there is a weird bug in the file `recursive_trinity.cmds`
        # where unexpected parameters are introduced into the commands
        "Trinity --max_memory 64G --CPU {threads} --SS_lib_type {params.sslibtype} --genome_guided_bam {input.sortbam} --genome_guided_max_intron 200 --output {params.prefix} --grid_exec \"{params.hpcgridrunnerpath} -G {output.hpcgridrunnerconfig} -c\";"
"""
